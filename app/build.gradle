apply plugin: 'com.android.application'

/* set the versionCode based on how many commits in the repo */
def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

/* gets the version name from the latest Git tag */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0"

    aaptOptions {
        cruncherEnabled = false
    }

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 201
        versionName getVersionName()
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError true

        htmlReport true
        xmlReport false
        textReport false

        warning 'MissingTranslation'
        // PackageManager.GET_DISABLED_COMPONENTS was removed in 24
        warning 'WrongConstant'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
}
